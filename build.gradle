buildscript {
    repositories { jcenter() }
    dependencies {
        classpath 'com.github.jengelman.gradle.plugins:shadow:+'
    }
}

apply plugin: 'java'
apply plugin: 'application'
apply plugin: 'maven'
apply plugin: 'maven-publish'
apply plugin: 'signing'
apply plugin: 'idea'
apply plugin: 'com.github.johnrengelman.shadow'

group = 'io.honeybadger'
version = '1.0.9'
sourceCompatibility = 1.7
targetCompatibility = 1.7
archivesBaseName = rootProject.name
mainClassName = "io.honeybadger.reporter.HoneybadgerCLI"

task wrapper(type: Wrapper) {
    gradleVersion = '2.4'
    distributionUrl = "https://services.gradle.org/distributions/gradle-$gradleVersion-all.zip"
}

run {
    standardInput = System.in
}

repositories {
    mavenCentral()
    mavenLocal()
}

configurations {
    providedCompile
    providedCompile.extendsFrom(compile)
}

javadoc {
    classpath = sourceSets.main.output + sourceSets.main.compileClasspath +
            configurations.providedCompile
}

sourceSets {
    main {
        compileClasspath += configurations.providedCompile
    }
    test {
        compileClasspath += configurations.providedCompile
        runtimeClasspath += configurations.providedCompile
    }
}

dependencies {
    // We compile against the servlet API, but we don't bring it into the dependency graph
    // because the implementation is typically provided by the application server
    providedCompile group: 'javax.servlet', name: 'javax.servlet-api',
            version: '3.1.0'

    // logging facade that allows users of the library to use their own logging implementations
    compile 'org.slf4j:slf4j-api:1.7.12'
    // JSON processing
    compile 'com.google.code.gson:gson:2.3.1'

    // HTTP requesting library - used to talk to the Honeybadger API
    compile('org.apache.httpcomponents:fluent-hc:4.5') {
        // we exclude commons logging because we replace it with a shadowed
        // jcl-over-slf4j in order to redirect all logging messages over
        // the slf4j interface
        exclude group: 'commons-logging'
    }

    // Used so we don't have to pull in commons-logging as a dependency
    compile 'org.slf4j:jcl-over-slf4j:1.7.12'

    compile 'com.jcabi:jcabi-manifests:1.1'

    // Utilities for testing below

    testCompile ('com.google.guava:guava:18.0') {
        exclude group: 'org.slf4j'
    }

    testCompile 'junit:junit:4.12'
    testCompile 'org.slf4j:slf4j-simple:1.7.12'
    testCompile 'org.mockito:mockito-all:1.10.19'
}

jar {
    onlyIf { false }
    doFirst {
        manifest {
            attributes("Honeybadger-Java-Version": version)
        }
    }
}

shadowJar {
    baseName = "${rootProject.name}"
    classifier = ''

    doFirst {
        manifest {
            attributes("Honeybadger-Java-Version": version)
        }
    }

    relocate 'com.google', 'io.honeybadger.com.google'
    relocate 'org.apache.httpcomponents', 'io.honeybadger.org.apache.httpcomponents'
    relocate 'org.apache.commons.codec', 'io.honeybadger.org.apache.commons.codec'
    relocate 'org.apache.commons.logging', 'io.honeybadger.org.apache.commons.logging'
    relocate 'com.jcabi', 'io.honeybadger.com.jcabi'
}

def installer = install.repositories.mavenInstaller

// TODO: Remove everything but slf4j
[installer]*.pom*.whenConfigured {pom ->
    pom.dependencies.retainAll {
        (it.artifactId == 'slf4j-api' && it.groupId == 'org.slf4j') ||
            it.scope != "compile"
    }
}

publishing {
    publications {
        shadow(MavenPublication) {
            from components.shadow
            artifactId = "${rootProject.name}"
        }
    }
}

jar.dependsOn(shadowJar)

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allJava
}

task javadocJar(type: Jar) {
    classifier = 'javadoc'
    from javadoc
}

// Dumps system properties to STDOUT
task showSystemProperties << {
    System.properties.each { k, v -> println "${k}:${v}" }
}

tasks.withType(Test) {
    def apiKeyProp = "honeybadger.api_key"

    if (!System.getProperty(apiKeyProp) && !System.getenv("HONEYBADGER_API_KEY")) {
        println 'No API key'
        def msg = "${apiKeyProp} system property must be specified"
        throw new org.gradle.api.InvalidUserDataException(msg)
    }

    // Assume a default environment of test if it isn't specified
    systemProperty 'ENV', System.getProperty('ENV', 'TEST')
    systemProperty 'honeybadger.version', version

    def passSysProp = { String prop ->
        systemProperty prop, System.getProperty(prop)
    }

    // White-listed system properties that can be passed from gradle.properties
    passSysProp(apiKeyProp)
    passSysProp('honeybadger.url')
    passSysProp('http.proxyHost')
    passSysProp('http.proxyPort')
    passSysProp('honeybadger.excluded_exception_classes')
    passSysProp('honeybadger.excluded_sys_props')
}

artifacts {
    archives javadocJar, sourcesJar, shadowJar
}

// TODO: Set me up with a new username for honeybadger.io
/*

signing {
    sign configurations.archives
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java

            artifact sourcesJar {
                classifier "sources"
            }

            // This messy thing is here to add proper exclusions to
            // generated POMs
            project.configurations[JavaPlugin.RUNTIME_CONFIGURATION_NAME].allDependencies.findAll {
                it instanceof ModuleDependency && !it.excludeRules.isEmpty()
            }.each { ModuleDependency dep ->
                pom.withXml {
                    def xmlDep = asNode().dependencies.dependency.find {
                        it.groupId[0].text() == dep.group && it.artifactId[0].text() == dep.name
                    }
                    def xmlExclusions = xmlDep.exclusions[0]
                    if (!xmlExclusions) xmlExclusions = xmlDep.appendNode('exclusions')

                    dep.excludeRules.each { ExcludeRule rule ->
                        def xmlExclusion = xmlExclusions.appendNode('exclusion')
                        xmlExclusion.appendNode('groupId', rule.group)
                        xmlExclusion.appendNode('artifactId', rule.module)
                    }
                }
            }
        }
    }
}

uploadArchives {
    repositories {
        mavenDeployer {
            beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

            repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
                authentication(userName: ossrhUsername, password: ossrhPassword)
            }

            snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots/") {
                authentication(userName: ossrhUsername, password: ossrhPassword)
            }

            pom.project {
                name 'Honeybadger JVM Client'
                packaging 'jar'
                description 'Library that sends exceptions to the online error management service Honeybadger.'
                url 'https://github.com/honeybadger-io/honeybadger-jvm-client-v2'

                scm {
                    connection 'scm:git:git@github.com:honeybadger-io/honeybadger-jvm-client-v2.git'
                    developerConnection 'scm:git:git@github.com:honeybadger-io/honeybadger-jvm-client-v2.git'
                    url 'git@github.com:honeybadger-io/honeybadger-jvm-client-v2.git'
                }

                licenses {
                    license {
                        name 'MIT License'
                        url 'https://raw.githubusercontent.com/honeybadger-io/honeybadger-jvm-client-v2/master/LICENSE'
                    }
                }

                developers {
                    developer {
                        id 'elijah'
                        name 'Elijah Zupancic'
                        email 'elijah@zupancic.name'
                        organizationUrl 'https://github.com/dekobon'
                    }
                }
            }
        }
    }
}
*/
idea {
    module {
        scopes.PROVIDED.plus += [configurations.providedCompile]
    }
}
